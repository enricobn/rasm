pub enum Option<T> {
  Some(value : T),
  None
}

pub fn toList<T>(o: Option<T>) -> List<T> {
    match(o, { v -> listOf(v);}, { -> List::Empty();});
}

pub fn print<T>(o: Option<T>) {
    match(o, { v -> print("Some("); print(v); print(")");}, { -> print("None");});
}

pub fn getOrElse<T>(o: Option<T>, default: T) -> T {
    match(o, { v -> v;}, { -> default;});
}

pub fn run<T>(o: Option<T>, f: fn(T) -> ()) {
    match(o, f, { -> });
}

pub fn map<T,T1>(o: Option<T>, f: fn(T) -> T1) -> Option<T1> {
    match(o, { v -> Option::Some(f(v)); }, { -> Option::None();});
}

pub fn flatten<T>(o: Option<Option<T>>) -> Option<T> {
    match(o, { v -> v; }, { -> Option::None(); });
}

pub fn fmap<T,T1>(o: Option<T>, f: fn(T) -> Option<T1>) -> Option<T1> {
    flatten(map(o, f));
}

pub fn or<T>(o: Option<T>, o1: Option<T>) -> Option<T> {
    match(o, { v -> o; }, { -> o1; });
}

pub fn orElse<T>(o: Option<T>, f: fn() -> Option<T>) -> Option<T> {
    match(o, { v -> o; }, f);
}

pub inline native toUnit<T>(o: Option<T>) /{
}/

pub fn isSome<T>(o: Option<T>) -> bool {
    o.match({ v -> true; }, { -> false; });
}