pub struct TestSuite {
    name: str,
    tests: Vec<RunTest>,
    suites: Vec<TestSuite>
}

pub fn TestSuite(name: str) -> TestSuite {
    TestSuite(name, Vec(), Vec());
}

pub fn add(suite: TestSuite, name: str, test: fn() -> Assertions) -> TestSuite {
    suite.tests(fn(it) { it.push(RunTest(name, test));});
}

pub fn add(suite: TestSuite, s: TestSuite) -> TestSuite {
    suite.suites(fn(it) { it.push(s);});
}

pub fn run(suite: TestSuite, test: Option<str>) -> bool {
    println("Running suite ", suite.name());
    
    let suitesResult = suite.suites.foldLeft(false, fn(prev, it) { prev.or(it.run(test)); });

    let testsResult = suite.tests
        .filter(fn(it) { 
            test.match( fn(gen) { gen.eq(it.name()); }, { true; });})
        .foldLeft(false, fn(prev,it) { prev.or(it.run()); });

    if (suitesResult.or(testsResult), 
        { println(suite.name, " \033[31mTests failed\033[0m"); }, 
        {});

    suitesResult.or(testsResult);
}

pub struct Assertions {
    failed: Vec<str>
}

pub struct RunTest {
   name: str,
   function: fn() -> Assertions
}

pub fn run(test: RunTest) -> bool {
    runTest(test.name, test.function);
}

pub fn Assertions() -> Assertions {
    Assertions(Vec());
}

pub fn fail(assertions: Assertions, message: str) -> Assertions {
    assertions.failed(assertions.failed.push(message));
}

pub fn validate(assertions: Assertions) {
    assertions.failed.first.match(fn(error) { panic("Assertion error: ".append(error)); }, {});
}

pub fn assertTrue(assertions: Assertions, value: bool) -> Assertions {
    if(value, {assertions; },{
        assertions.fail("assertion failed");
    });
}

pub fn assertFalse(assertions: Assertions, value: bool) -> Assertions {
    if(value,{
        assertions.fail("assertion failed");
    }, {assertions; });
}

pub fn assertTrue(assertions: Assertions, message: str, value: bool) -> Assertions {
    if(value,{assertions; },{
        assertions.fail("assertion failed: ".add(message));
    });
}

pub fn assertFalse(assertions: Assertions, message: str, value: bool) -> Assertions {
    if(value, {
        assertions.fail("assertion failed: ".add(message));
    }, {assertions; });
}

pub fn assertEquals<T>(assertions: Assertions, message: str, expected: T, current: T) -> Assertions {
    if(expected.eq(current).not, {
        assertions.fail("assertion failed: ".add(message).add(". Expected ").append(expected).append(" but found ").append(current));
    }, {assertions; });
}

pub fn assertEquals<T>(assertions: Assertions, expected: T, current: T) -> Assertions {
    if(expected.eq(current).not, {
        assertions.fail("assertion failed, expected ".append(expected).append(" but found ").append(current));
    }, {assertions; });
}

pub fn runTest(name: str, testRun: fn() -> Assertions) -> bool {
    print(name);
    let startTime = now();
    let runResult = testRun();
    let elapsedTime = elapsedTime(now(), startTime);
    print(" - ");
    print(elapsedTime.div(1000));
    print(" ms");

    runResult.failed.first.match(
        fn(error) {
            let message = " - \033[31mFAILED\033[0m : ";
            print(message);
            println(error);
            true;
        },
        {
            println(" - \033[32mok\033[0m");
            false;
        }
    );
}