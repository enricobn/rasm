pub enum Option<T> {
  Some(value : T),
  None
}

pub fn toList<T>(o: Option<T>) -> List<T> {
    match(o, fn(v) {listOf(v);}, {List::Empty();});
}

pub fn print<T>(o: Option<T>) {
    match(o, fn(v) { print("Some("); print(v); print(")");}, {print("None");});
}

pub fn getOrElse<T>(o: Option<T>, defaultValue: T) -> T {
    match(o, fn(v) {v;}, {defaultValue;});
}

pub fn run<T>(o: Option<T>, f: fn(T) -> ()) {
    match(o, f, {});
}

pub fn map<T,T1>(o: Option<T>, f: fn(T) -> T1) -> Option<T1> {
    match(o, fn(v) {Option::Some(f(v)); }, {Option::None();});
}

pub fn flatten<T>(o: Option<Option<T>>) -> Option<T> {
    match(o, fn(v) {v;}, {Option::None(); });
}

pub fn fmap<T,T1>(o: Option<T>, f: fn(T) -> Option<T1>) -> Option<T1> {
    flatten(map(o, f));
}

pub fn or<T>(o: Option<T>, o1: Option<T>) -> Option<T> {
    match(o, fn(v) { o; }, { o1; });
}

pub fn orElse<T>(o: Option<T>, f: fn() -> Option<T>) -> Option<T> {
    match(o, fn(v) {o; }, f);
}

pub fn isSome<T>(o: Option<T>) -> bool {
    o.match(fn(v) {true; }, {false; });
}

pub fn toString<T>(o: Option<T>) -> str {
    o.match( fn(value) {"Some(".add(value).add(")"); }, {"None"; });
}