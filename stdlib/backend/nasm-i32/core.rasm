extern "exit"

asm exitMain(status: i32) /{
    %ifdef LIBC
    push    dword $status
    call    exit
    %else
    push    dword ebx
    mov     ebx, $status    ; Arg one: the status
    mov     eax, 1          ; Syscall number: 1
    int     0x80
    pop     ebx
    %endif
}/

asm argc() -> i32 /{
    mov     eax,[_rasm_args]
}/

// TODO unsafe use Option
asm argv(i: i32) -> str /{
    push edx ; since the mul function stores the result even in edx
    push ebx
    push ecx
    mov ebx, _rasm_args
    mov ecx,4           ; i add 4*i to the base address (_rasm_args)
    mov eax,$i
    inc eax             ; we skip the length (argc)
    mul ecx
    add ebx,eax
    mov eax,[ebx]
    pop ecx
    pop ebx
    pop edx
}/

asm memcopy(source: i32, dest: i32, len: i32) /{
    push    ecx
    push    esi
    push    edi

    mov     esi, $source
    mov     edi, $dest
    mov     ecx, $len
    rep     movsb

    pop     edi
    pop     esi
    pop     ecx
}/

asm malloc(size: i32, descr: str) -> i32 /{
    push    ebx
    push    ecx
    push    edx

    %ifdef LOG_DEBUG
    $call(print, "start malloc ")
    $call(print, $size)
    $call(print, ", ")
    $call(println, $descr)
    %endif

    mov     dword ecx, _reusable_heap_table

    %ifdef LOG_DEBUG
    mov     dword ebx, [_reusable_heap_table_next]
    sub     ebx,ecx
    $call(print, "  reusable size ")
    $call(println, ebx)
    %endif

    mov dword ebx,$size
    mov dword edx,[_reusable_heap_table_next]
.reusable_loop:
    cmp     dword ecx,edx
    je      .allocation

    mov     eax,[ecx]
    cmp     dword [eax + 8],ebx
    je     .reusable_good
    add     ecx,4
    jmp    .reusable_loop
.reusable_good:
    %ifdef LOG_DEBUG
    $call(print, "  reusing address ")
    $call(print, eax)
    $call(print, ", size ")
    $call(println, [eax + 8])
    ; $call(checkAddress,eax)
    %endif

    $call(removeFromReused,ecx)
    mov     dword [eax + 4], 1      ; allocated flag
    mov     dword [eax + 12], 0     ; count
    mov     dword [eax + 16], 0     ; reusable table
    jmp     .end

.allocation:
    %ifdef LOG_DEBUG
    $call(println, "  allocation ")
    %endif

    mov     dword eax,[_heap_table_next]; the pointer to the heap table
    mov     dword edx,[_heap_table_size]
    add     dword edx,_heap_table
    cmp     dword eax,edx
    jb     .loop_ok
    $call(println, "Out of heap space")
    $call(exitMain, 1)

.loop_ok:
    mov     dword eax,[_heap_table_next]
    %ifdef LOG_DEBUG
    $call(println, "  new address")
    %endif

    mov     dword ecx,[_heap]
    sub     ecx,_heap_buffer
    cmp     ecx,[_heap_size]
    jb      .new_address_ok
    $call(println, "Out of memory")
    $call(exitMain, 1)

.new_address_ok:
    %ifdef LOG_DEBUG
    $call(println, "  newAddress Ok")
    %endif

    mov     dword ecx,[_heap]
    mov     dword [eax],ecx
    mov     dword edx, $size
    add     ecx, edx
    mov     dword [_heap],ecx
    mov     dword [eax + 5 * 4],0 ; to be sure that the next slot is considered free

    %ifdef LOG_DEBUG
    $call(println, "  allocate")
    %endif

    mov     dword [eax + 4], 1      ; allocated flag
    mov     dword [eax + 8], edx    ; size
    mov     dword [eax + 12], 0     ; count
    mov     dword [eax + 16], 0     ; reusable table
    add     eax, 20
    mov     [_heap_table_next],eax
    sub     eax, 20

.end:
    ;mov     dword eax,[eax]

    %ifdef LOG_DEBUG
    $call(print, "  ")
    $call(println, eax)
    $call(println, "end malloc")
    %endif

    pop     edx
    pop     ecx
    pop     ebx
}/

asm addStaticStringToHeap(s: i32) -> i32 /{
    push    ebx
    push    ecx
    push    edx

    ; we can't print here
    ; mov     dword eax,[_heap_table_next]; the pointer to the heap table
    ; mov     dword edx,[_heap_table_size]
    ; add     edx,_heap_table
    ; cmp     eax,edx
    ; jb     .ok
    ; $call(println, "Out of heap space")
    ; $call(exitMain, 1)

.ok:
    mov     dword eax,[_heap_table_next]

    mov     dword ecx,$s
    mov     dword [eax],ecx
    mov     dword [eax + 5 * 4],0 ; to be sure that the next slot is considered free

    mov     dword [eax + 4], 1      ; allocated flag
    mov     dword [eax + 8], 0      ; size
    mov     dword [eax + 12], 1     ; count
    mov     dword [eax + 16], 0     ; reusable table
    add     eax, 20
    mov     [_heap_table_next],eax
    sub     eax, 20

.end:
    pop     edx
    pop     ecx
    pop     ebx
}/

asm deref(address: i32, descr: str) -> i32 /{
    push    eax
    push    ebx

    %ifdef LOG_DEBUG
    $call(print, "start deref ")
    $call(print, $address)
    $call(print, "\n")
    $call(print, "  ")
    $call(print, $descr)
    $call(print, "\n")
    %endif

    %ifdef LOG_DEBUG
    mov     dword eax, _reusable_heap_table
    mov     dword ebx, [_reusable_heap_table_next]
    sub     ebx,eax
    $call(print, "  reusable size ")
    $call(print, ebx: i32)
    $call(print, "\n")
    %endif

    %ifdef LOG_DEBUG
    ; we check if the address is valid
    mov     dword eax,_heap_table
    cmp     dword $address,eax
    jb     .invalid_address
    add     eax,[_heap_table_size]
    cmp     dword $address,eax
    jg      .invalid_address
    %endif

    mov     dword eax,$address
    mov     dword ebx,[eax + 12] ; the count

    %ifdef LOG_DEBUG
    test    ebx,ebx
    jz      .negative_count
    %endif

    sub     ebx,1

    %ifdef LOG_DEBUG
    $call(print, "  ref count: ")
    $call(print, ebx: i32)
    $call(print, "\n")
    %endif

    mov     dword [eax + 12],ebx
    test    ebx,ebx
    jnz     .end

    %ifdef LOG_DEBUG
    $call(print, "  deallocated ")
    $call(print, eax)
    $call(print, "\n")
    %endif

    mov     dword [eax + 4],0; allocated false
    mov     dword ebx,[_reusable_heap_table_next]
    mov     dword [eax + 16],ebx; reusable table
    mov     dword [ebx],eax

    %ifdef LOG_DEBUG
    $call(print, "  add reused ", ebx);
    $call(print, " with value ", [ebx]);
    %endif

    add     ebx,4
    mov     dword [_reusable_heap_table_next],ebx

    jmp     .end
.negative_count:
    $call(print, "Negative allocation count: ");
    $call(println, $descr)
    $call(exitMain, 1);
.invalid_address:
    $call(invalidAddress)
.end:
    %ifdef LOG_DEBUG
    $call(print, "end deref\n")
    %endif

    pop     ebx
    pop     eax
}/

/**
 * given an address in the allocation table, dereferences that entry and adds it to the reusable table
 */
asm freeMem(address: i32) /{
    push    ebx
    push    eax

    mov     eax, $address
    mov     dword [eax + 4],0; allocated false
    mov     dword ebx,[_reusable_heap_table_next]
    mov     dword [eax + 16],ebx; reusable table
    mov     dword [ebx],eax
    add     ebx,4
    mov     dword [_reusable_heap_table_next],ebx

    pop     eax
    pop     ebx
}/

asm addRef(address: i32, descr: str) /{
    push    eax
    push    ebx

    %ifdef LOG_DEBUG
    $call(print, "start addRef ")
    $call(print, $address)
    $call(print, "\n")
    $call(print, "  ")
    $call(print, $descr)
    $call(print, "\n")
    %endif

    %ifdef LOG_DEBUG
    push    ecx
    mov     dword ecx, _reusable_heap_table
    mov     dword ebx, [_reusable_heap_table_next]
    sub     ebx,ecx
    $call(print, "  reusable size ")
    $call(print, ebx)
    $call(print, "\n")
    pop     ecx
    %endif

    %ifdef LOG_DEBUG
    ; we check if the address is valid
    mov     dword eax,_heap_table
    cmp     $address,eax
    jb     .invalid_address
    add     eax,[_heap_table_size]
    cmp     $address,eax
    jg      .invalid_address
    %endif

    mov     dword eax,$address

    mov     dword ebx,[eax + 12] ; the count
    add     ebx,1

    %ifdef LOG_DEBUG
    $call(print, "  ref count: ")
    $call(print, ebx)
    $call(print, "\n")
    %endif

    mov     dword [eax + 12],ebx

    mov     dword ebx,[eax + 16]
    test    ebx,ebx                ; no reusable_table
    jz     .allocate

    $call(removeFromReused, ebx)
    mov     dword [eax + 16],0

.allocate:
    mov     dword [eax + 4],1   ; allocated true
    jmp     .end
.invalid_address:
    $call(invalidAddress)
.end:
    %ifdef LOG_DEBUG
    $call(print, "end addRef\n")
    %endif

    pop     ebx
    pop     eax
}/

asm removeFromReused(address: i32) /{
    push    eax

    %ifdef LOG_DEBUG
    $call(print, "  remove from reused ")
    $call(print, $address)
    $call(print, " ")
    mov     dword eax, $address
    $call(print, [eax])
    %endif

    mov     dword eax,[_reusable_heap_table_next]
    sub     eax,4
    mov     dword [_reusable_heap_table_next],eax

    %ifdef LOG_DEBUG
    $call(print, " ")
    $call(println, eax)
    %endif

    cmp     dword eax,$address
    jbe     .end

    push    ebx

    mov     dword eax,[eax]      ; the pointer to the allocation table that we want to move
    mov     dword ebx,$address   ; the pointer to the reusable table that we want to delete
    mov     dword [ebx],eax      ; we move the pointer to the allocation table that we want to move, to the reusable table that we want to delete
    mov     dword [eax + 16],ebx ; we must update the pointer in the allocation table

    pop     ebx

.end:
    pop     eax
}/

fn invalidAddress() {
    print("invalid address \n");
    exitMain(1);
}

asm printAllocated() /{
    push    eax
    push    ebx
    push    ecx
    push    edx

    mov     dword eax,_heap_table
    mov     dword ebx,0
    mov     dword ecx,0
.loop:
    cmp     dword ebx,[_heap_table_size]
    jge     .end
    mov     dword edx,[eax + 4] ; 1 == allocated
    cmp     dword edx,0
    jz      .not_allocated
    add     dword ecx,[eax + 8] ; size
.not_allocated:
    add     eax, 5 * 4
    add     ebx, 5 * 4
    jmp     .loop
.end:
    $call(print, ecx)
    $call(println, " bytes allocated")

    pop     edx
    pop     ecx
    pop     ebx
    pop     eax
}/

/* probably there's a faster way to do this: a simple diff... */
asm printTotalMemory() /{
    push    eax
    push    ebx
    push    ecx
    push    edx

    mov     dword eax,_heap_table
    mov     dword ebx,0
    mov     dword ecx,0
.loop:
    cmp     dword ebx,[_heap_table_size]
    jge     .end
    mov     dword edx,[eax + 4]; 1 == allocated
    add     dword ecx,[eax + 8] ; size
    add     eax, 5 * 4
    add     ebx, 5 * 4
    jmp     .loop
.end:
    $call(print, ecx)
    $call(println, " bytes of total memory")

    pop     edx
    pop     ecx
    pop     ebx
    pop     eax
}/

asm printTableSlotsAllocated() /{
    push    eax
    push    ebx

    mov     dword eax,0
    mov     dword ebx,_heap_table
.loop:
    cmp     dword [ebx],0
    jz      .end
    add     eax,1
    add     ebx,5 * 4
    jmp     .loop
.end:
    $call(print, eax)
    $call(println, " table slots allocated")

    pop     ebx
    pop     eax
}/

fn derefAddressNotFound() {
    println("Deref: address not found");
    exitMain(1);
}

/* checks if the address is used somewhere in memory, in that case it exits */
asm checkAddress(address: i32) /{
    push    eax
    push    ebx
    push    ecx
    push    edx

    $call(println, "  checkAddress")

    mov     dword eax,_heap_table
    mov     dword ebx,0
    mov     dword ecx,0
.loop:
    cmp     dword ebx,[_heap_table_size]
    jge     .end
    mov     dword edx,[eax + 4]; 1 == allocated
    cmp     dword edx,0
    jz      .not_allocated
    $call(memContains, [eax], [eax +8], $address)
.not_allocated:
    add     eax, 5 * 4
    add     ebx, 5 * 4
    jmp     .loop
.end:
    pop     edx
    pop     ecx
    pop     ebx
    pop     eax
}/

asm memContains(startAddress: i32, len: i32, what: i32) /{
    push    eax
    push    ebx
    push    ecx
    ; $call(print, "    memContains(")
    ; $call(print, $startAddress)
    ; $call(print, ", ")
    ; $call(print, $len)
    ; $call(print, ", ")
    ; $call(print, $what)
    ; $call(println, ")")

    mov     dword eax,$startAddress
    mov     dword ebx,$len
.loop:
    cmp     dword ebx,0
    jbe     .end
    mov     dword ecx,[eax]
    cmp     dword ecx,$what
    jne     .not_equals
    $call(println, "Address is still in use")
    $call(exitMain, 1)
.not_equals:
    add     eax, 1
    sub     ebx, 1
    jmp     .loop
.end:
    pop     ecx
    pop     ebx
    pop     eax
}/

asm copy<T>(o: T) -> T /{
    push    ebx
    push    ecx

    mov     dword ebx,$o
    mov     dword ecx,[ebx + 8] ; size
    $call(malloc, ecx, "for clone")
    $call(memcopy, [ebx], [eax], ecx)
    ; $addRef(eax:T)
    ; $call(deref,eax:i32, "deref for clone")

    pop     ecx
    pop     ebx
}/

asm addStaticAllocation(labelAllocation: i32, labelMemory: i32, size: i32) /{
    push    ebx
    mov     dword ebx, $labelMemory

    mov     dword eax, $labelAllocation
    mov     dword [eax], ebx
    mov     dword [eax + 4], 1      ; allocated flag
    mov     dword ebx, $size
    mov     dword [eax + 8], ebx  ; size
    mov     dword [eax + 12], 1     ; count
    mov     dword [eax + 16], 0     ; reusable table

    pop     ebx
}/

asm addHeap(label: i32, descrLabel: i32, value: i32) /{
    push    ebx

    mov     dword ebx, $descrLabel
    $call(malloc, 4, [ebx]: str)
    $call(addRef, eax, [ebx]: str)
    mov     dword ebx, $label
    mov     dword [ebx], eax
    mov     dword eax, [eax]
    mov     dword ebx, $value
    mov     dword [eax], ebx

    pop   ebx
}/