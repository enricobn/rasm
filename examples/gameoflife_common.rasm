struct Cell {
    x: i32,
    y: i32
}

struct FullCell {
    cell: Cell,
    adjacents: List<Cell>
}

fn allCells(width: i32, height: i32) -> List<FullCell> {
    zipList(range(0, height), range(0, width), { y, x ->
        let cell = Cell(x, y);

        FullCell(cell, adjacents(cell));
    });
}

fn moveCells(cells: List<Cell>, d: i32) -> List<Cell> {
    map(cells, { it -> Cell(nadd(Cell::x(it), d), nadd(Cell::y(it), d)); });
}

fn next(cells: List<Cell>, allCells: List<FullCell>) -> List<Cell> {
    map(listFilter(allCells, { it ->
        let adjsAlive = listCount(FullCell::adjacents(it),  { a -> isAlive(cells, a); });
        ifImmediate(isAlive(cells, FullCell::cell(it)),
            or(eq(adjsAlive, 2),eq(adjsAlive,3)),
            eq(adjsAlive, 3)
        );
    }), { it -> FullCell::cell(it);});
}

fn adjacents(cell: Cell) -> List<Cell> {
    let x = Cell::x(cell);
    let y = Cell::y(cell);

    let left = nadd(x, -1);
    let right = nadd(x, 1);
    let up = nadd(y, -1);
    let down = nadd(y, 1);

    listOf8(
        Cell(left, up),
        Cell(left, y),
        Cell(left, down),
        Cell(x, up),
        Cell(x, down),
        Cell(right, up),
        Cell(right, y),
        Cell(right, down)
    );
}

/** false = 0 true = any other value **/
fn isAlive(cells: List<Cell>, cell: Cell) -> bool {
    let x = Cell::x(cell);
    let y = Cell::y(cell);

    listAnyMatch(cells, { it ->
        and(eq(x, Cell::x(it)), eq(y, Cell::y(it)));
    });
}

fn readCellFile(file: str) -> List<Cell> {
    let linesEnumerate = listEnumerate(filterLines(lines(readFile(file))));

    fmap(linesEnumerate, { line -> lineCells(line); });
}

fn lineCells(line: EnumerateEntry<str>) -> List<Cell> {
    let y = EnumerateEntry::index(line);
    let lineStr = EnumerateEntry::value(line);

    map(listFilter(listEnumerate(chars(lineStr)), { cell ->
        let cellStr = EnumerateEntry::value(cell);

        List::match(chars(cellStr), { head, tail -> seq(head, "O"); }, { -> false;});
    }), { cell -> Cell(EnumerateEntry::index(cell), y); });
}

fn filterLines(lines: List<str>) -> List<str> {
    listFilter(lines, { line ->
        List::match(chars(line), { head, tail -> not(seq(head, "!")); }, { -> false;});
    });
}