struct Cell {
    x: i32,
    y: i32
}

struct FullCell {
    cell: Cell,
    adjacents: List<Cell>
}

fn allCells(width: i32, height: i32) -> List<FullCell> {
    cartesianProduct(range(0, height), range(0, width), { y, x ->
        let cell = Cell(x, y);

        FullCell(cell, adjacents(cell));
    });
}

fn moveCells(cells: List<Cell>, d: i32) -> List<Cell> {
    map(cells, { it -> Cell(add(Cell::x(it), d), add(Cell::y(it), d)); });
}

fn next(cells: List<Cell>, allCells: List<FullCell>) -> List<Cell> {
    map(filter(allCells, { it ->
        let adjsAlive = count(FullCell::adjacents(it),  { a -> isAlive(cells, a); });
        ifImmediate(isAlive(cells, FullCell::cell(it)),
            or(eq(adjsAlive, 2),eq(adjsAlive,3)),
            eq(adjsAlive, 3)
        );
    }), { it -> FullCell::cell(it);});
}

fn adjacents(cell: Cell) -> List<Cell> {
    let x = Cell::x(cell);
    let y = Cell::y(cell);

    let left = add(x, -1);
    let right = add(x, 1);
    let up = add(y, -1);
    let down = add(y, 1);

    listOf(
        Cell(left, up),
        Cell(left, y),
        Cell(left, down),
        Cell(x, up),
        Cell(x, down),
        Cell(right, up),
        Cell(right, y),
        Cell(right, down)
    );
}

/** false = 0 true = any other value **/
fn isAlive(cells: List<Cell>, cell: Cell) -> bool {
    let x = Cell::x(cell);
    let y = Cell::y(cell);

    anyMatch(cells, { it ->
        and(eq(x, Cell::x(it)), eq(y, Cell::y(it)));
    });
}

fn readCellFile(file: str) -> List<Cell> {
    let linesEnumerate = listEnumerate(filterLines(lines(readFile(file))));

    fmap(linesEnumerate, { line -> lineCells(line); });
}

fn lineCells(line: EnumerateEntry<str>) -> List<Cell> {
    let y = EnumerateEntry::index(line);
    let lineStr = EnumerateEntry::value(line);

    map(filter(listEnumerate(chars(lineStr)), { cell ->
        let cellStr = EnumerateEntry::value(cell);

        match(chars(cellStr), { head, tail -> seq(head, "O"); }, { -> false;});
    }), { cell -> Cell(EnumerateEntry::index(cell), y); });
}

fn filterLines(lines: List<str>) -> List<str> {
    filter(lines, { line ->
        match(chars(line), { head, tail -> not(seq(head, "!")); }, { -> false;});
    });
}