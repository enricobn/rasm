// run with: time ./gameoflife_sdl resources/examples/f116.cells 100
//
// 481236 bytes allocated
// 138099 table slots allocated
//
// real    0m21,196s

sdlInit();

let file = argv(1);
let start = readCellFile(file);

toInt(argv(2)).match(fn(count) {
    run(moveCells(start, 20), 80, 50, count);
    sdlQuit();
}, { println("Cannot parse count"); });

fn run(start: List<Cell>, width: i32, height: i32, count: i32) {
    let win = sdlErrorIfNull(sdlCreateWindow("Game of life", 100, 100, 1024, 768), "SDL_CreateWindow Error: %s\n");
    let ren = sdlErrorIfNull(sdlCreateRenderer(win), "SDL_CreateRenderer Error: %s\n");

    sdlSetRenderDrawColor(ren, 0, 0, 0, 0);
    sdlRenderClear(ren);
    sdlSetRenderDrawColor(ren, 255, 255, 255, 0);

    let allCells = allCells(width, height);

    printCells(ren,
        foldLeft(range(0, count), start, fn(prev, act) {
            printCells(ren, prev, width, height);
            //sdlDelay(1000);
            next(prev, allCells);
          }), width, height);

    sdlDestroyRenderer(ren);
    sdlDestroyWindow(win);
}

fn printCells(ren: SDLRenderer, cells: List<Cell>, width: i32, height: i32) {
    sdlSetRenderDrawColor(ren, 0, 0, 0, 0);
    sdlRenderClear(ren);
    sdlSetRenderDrawColor(ren, 255, 0, 0, 0);
    sdlRenderFillRect(ren, SDLRect(0, 0, mul(10, width), mul(10, height)));
    sdlSetRenderDrawColor(ren, 255, 255, 255, 0);

    let xRange = range(0, width);

    forEach(range(0, height), fn(y) {
        forEach(xRange, fn(x) {
            if(isAlive(cells, Cell(x, y)), {
                 sdlRenderFillRect(ren, SDLRect(mul(10, x), mul(10, y), 10, 10));
            }, {});
        });
    });
    sdlRenderPresent(ren);
}