struct MenuState {
    highScores: Vec<HighScore>
}

fn update(resources: Resources, keys: Vec<i32>, menuState: MenuState) -> Stage {
    let spacePressed = count(keys, { k -> eq(k, 44);});

    if(eq(spacePressed, 0), { ->
        Stage::Menu(menuState);
    }, { ->
        Stage::Game(initialGameState());
    });
}

fn render(ren: SDLRenderer, resources: Resources, menuState : MenuState) {
    let font = font(resources);

    let title = createSDLText(ren, font, "Rasm BREAKOUT", GREEN);
    render(ren, title, SDLRect(380, 200, 200, 24));
    release(title);

    let highScores = take(sort(highScores(menuState), { h1, h2 ->
        let s1 = score(h1);
        let s2 = score(h2);
        cmp(s2, s1);
    }), 10);

    let last = first(sort(highScores(menuState), { h1, h2 ->
         let t1 = time(h1);
         let t2 = time(h2);
         cmp(t2, t1);
     }));

    forEach(enumerate(highScores), { enumerateEntry ->
        let index = index(enumerateEntry);
        let highScore = value(enumerateEntry);
        let score = score(highScore);
        let time = time(highScore);

        let color = match(last, { h ->
            let lastTime = time(h);
            if(eq(lastTime, time), YELLOW, BLUE);
        }, { -> BLUE;});

        let sScore = padLeft(toString(score), 10);
        let scoreText = createSDLText(ren, font, sScore, color);
        let y = add(300, mul(index, 30));
        render(ren, scoreText, SDLRect(380, y, mul(slen(sScore), 24), 24));
        release(scoreText);
    });

    let footer = createSDLText(ren, font, "Press SPACE to start", RED);
    render(ren, footer, SDLRect(380, 700, 200, 24));
    release(footer);
}