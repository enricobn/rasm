pub struct Ball {
    x: f32,
    y: f32,
    xDir: XDirection,
    yDir: YDirection,
    lastUpdate: TimeVal
}

pub fn initialBall(now: TimeVal) -> Ball {
    Ball(500.0, 200.0, Right(), Down(), now);
}

pub fn update(ball: Ball, actualTime: TimeVal) -> Ball {
    let elapsed = elapsedTime(actualTime, ball.lastUpdate);

    let diff = divf(elapsed, 3000);

    let updatedX = update(ball.x, ball.xDir, diff);
    let newX = updatedX.first;
    let newXDir = updatedX.second;

    let updatedY = update(ball.y, ball.yDir, diff);
    let newY = updatedY.first;
    let newYDir = updatedY.second;

    Ball(newX, newY, newXDir, newYDir, actualTime);
}

fn update(x: f32, xDir: XDirection, diff: f32) -> Pair<f32,XDirection> {
    let newX = x.move(xDir, diff);
    let rightMargin = sub(WIDTH, BALLSIZE);

    if (greater(newX, rightMargin),
        { Pair(rightMargin.sub(newX.sub(rightMargin)), flip(xDir)); },
        less(newX, 0),
        { Pair(sub(0.0, newX), flip(xDir)); },
        { Pair(newX, xDir);}
    );
}

fn update(y: f32, yDir: YDirection, diff: f32) -> Pair<f32,YDirection> {
    let newY = y.move(yDir, diff);
    if(less(newY, HEADERHEIGHT),
        { Pair(tof32(HEADERHEIGHT).add(tof32(HEADERHEIGHT).sub(newY)), flip(yDir));},
        { Pair(newY, yDir);}
    );
}

pub fn collides(ball: Ball, brick: Brick) -> bool {
    let bx = brick.x;
    let by = brick.y;

    let xColl = ball.x.add(BALLSIZE).greater(bx).and(ball.x.less(bx.add(BRICKWIDTH)));
    let yColl = ball.y.add(BALLSIZE).greater(by).and(ball.y.less(by.add(BRICKHEIGHT)));

    xColl.and(yColl);
}

pub fn collides(ball: Ball, barPos: f32) -> bool {
    let bx = barPos;
    let by = HEIGHT.sub(BARHEIGHT);

    let xColl = ball.x.add(BALLSIZE).greater(bx).and(ball.x.less(bx.add(BARWIDTH)));
    let yColl = ball.y.add(BALLSIZE).greater(by);

    xColl.and(yColl);
}

pub fn afterCollision(ball: Ball, brick: Brick) -> Ball {
    let xDiff1 = ball.x.sub(brick.x);
    let xDiff2 = brick.x.add(BRICKWIDTH).sub(ball.x);
    let yDiff1 = ball.y.sub(brick.y);
    let yDiff2 = brick.y.add(BRICKHEIGHT).sub(ball.y);

    let xDiff = min(xDiff1, xDiff2);
    let yDiff = min(yDiff1, yDiff2);

    if(xDiff.less(yDiff), { ball.xDir(ball.xDir.flip);}, { ball.yDir(ball.yDir.flip);});
}