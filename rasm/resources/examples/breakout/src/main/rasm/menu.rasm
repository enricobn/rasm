pub struct MenuState {
    highScores: Vec<HighScore>
}

pub fn update(resources: Resources, keys: Vec<int>, menuState: MenuState) -> Stage {
    let spacePressed = count(keys, eq.curry(44));

    if(eq(spacePressed, 0), {
        Menu(menuState);
    }, {
        Game(initialGameState());
    });
}

pub fn render(ren: SDLRenderer, resources: Resources, menuState : MenuState) {
    let font = resources.font;

    let title = sdlCreateText(ren, font, "Rasm BREAKOUT", COLORS.green);
    sdlRenderAndRelease(ren, title, 380, 200);

    let highScores = take(sort(highScores(menuState), fn(h1, h2) {
        let s1 = score(h1);
        let s2 = score(h2);
        cmp(s2, s1);
    }), 10);

    let last = first(sort(highScores(menuState), fn(h1, h2) {
         let t1 = time(h1);
         let t2 = time(h2);
         cmp(t2, t1);
     }));

    forEach(enumerate(highScores), fn(enumerateEntry) {
        let index = enumerateEntry.index;
        let highScore = enumerateEntry.value;
        let score = highScore.score;
        let time = highScore.time;

        let color = match(last, fn(h) {
            if(eq(h.time, time), COLORS.yellow, COLORS.blue);
        }, { COLORS.blue;});

        let sScore = padLeft(toString(score), 10);
        let scoreText = sdlCreateText(ren, font, sScore, color);
        let y = add(300, mul(index, 30));
        sdlRenderAndRelease(ren, scoreText, 450, y);
    });

    let footer = sdlCreateText(ren, font, "Press SPACE to start", COLORS.red);
    sdlRenderAndRelease(ren, footer, 380, 700);
}